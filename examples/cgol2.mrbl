
|frameRate
|	15
videoWidth
	256
videoHeight
	256
windowWidth
	768
windowHeight
	768


var liveColor B4
	B4from01  0.22  0.92  0.44  1.0
var deadColor B4
	B4from01  0.08  0.22  0.55  1.0

fn initialCellFiller N1
	x N1
	y N1
	sourceWidth  N1
	sourceHeight N1
	> 2 (% x 4)


@enter

fn neighborCount N1
	x N1
	y N1
	add8
		fromN1D2 (- x 1) (- y 1) cells
		fromN1D2    x    (- y 1) cells
		fromN1D2 (+ x 1) (- y 1) cells
		fromN1D2 (- x 1) (+ y 1) cells
		fromN1D2    x    (+ y 1) cells
		fromN1D2 (+ x 1) (+ y 1) cells
		fromN1D2 (- x 1)    y    cells
		fromN1D2 (+ x 1)    y    cells

fn cgolFiller N1
	x N1
	y N1
	w N1
	h N1
	cgolRule (neighborCount x y) (fromN1D2 x y cells)

fn cgolRule N1
	theNeighborCount N1
	current          N1
	?
		current
		?
			either
				> theNeighborCount 3
				< theNeighborCount 2
			0
			1
		?
			= 3 theNeighborCount
			1
			0



state cells N1D2
	?
		currentFrame
		mapN1D2 cgolFiller cells
		mapInN1D2
			initialCellFiller
			N4
				* 3 (/ videoWidth  8)
				* 3 (/ videoHeight 8)
				/ videoWidth  4
				/ videoHeight 4
			buildN1D2 videoWidth videoHeight


fn cellsToVideo B4
	x N1
	y N1
	sourceWidth  N1
	sourceHeight N1
	?
		fromN1D2 x y cells
		liveColor
		deadColor


videoOut
	mapB4D2
		cellsToVideo
		buildB4D2
			videoWidth
			videoHeight

next
	? (< currentFrame lastFrame) enter exit

@.


var lastFrame N1
	200

