
frameRate
	10
videoWidth
	256
videoHeight
	256
windowWidth
	512
windowHeight
	512


var liveColor byte4
	byte4From01  0.22  0.92  0.44  1.0
var deadColor byte4
	byte4From01  0.08  0.22  0.55  1.0

fn initialCellFiller num
	x num
	y num
	sourceWidth  num
	sourceHeight num
	> 1 (% x 3)


@enter

fn neighborCount num
	x num
	y num
	add8
		fromNumD2 (- x 1) (- y 1) cells
		fromNumD2    x    (- y 1) cells
		fromNumD2 (+ x 1) (- y 1) cells
		fromNumD2 (- x 1) (+ y 1) cells
		fromNumD2    x    (+ y 1) cells
		fromNumD2 (+ x 1) (+ y 1) cells
		fromNumD2 (- x 1)    y    cells
		fromNumD2 (+ x 1)    y    cells

fn cellFlipper num
	x num
	y num
	sourceWidth  num
	sourceHeight num
	?
		fromNumD2 x y cells
		?
			either
				> 2 (neighborCount x y)
				< 3 (neighborCount x y)
			0
			1
		?
			= 3 (neighborCount x y)
			1
			0



state cells numD2
	?
		currentFrame
		dynamicFillNumD2 cellFlipper cells
		limitedDynamicFillNumD2
			initialCellFiller
			num4
				* 3 (/ videoWidth  8)
				* 3 (/ videoHeight 8)
				/ videoWidth  4
				/ videoHeight 4
			buildNumD2 videoWidth videoHeight


fn cellsToVideo byte4
	x num
	y num
	sourceWidth  num
	sourceHeight num
	?
		fromNumD2 x y cells
		liveColor
		deadColor


videoOut
	dynamicFillByte4D2
		cellsToVideo
		buildByte4D2
			videoWidth
			videoHeight

