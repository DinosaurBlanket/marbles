

frameRate
	4
windowWidth
	128
windowHeight
	128


var liveColor byte4
	byte4From01  0.22  0.92  0.44  1.0
var deadColor byte4
	byte4From01  0.08  0.22  0.55  1.0



@enter

|fn neighborCount num
|	x num
|	y num
|	>
|		0
|		-
|			sumLimitedNumD2
|				num4 (- x 1) (- y 1) 3 3
|				cells
|			fromNumD2 x y cells

fn neighborCount num
	x num
	y num
	+
		fromNumD2 (- x 1) (- y 1) cells
		+
			fromNumD2    x    (- y 1) cells
			+
				fromNumD2 (+ x 1) (- y 1) cells
				+
					fromNumD2 (- x 1)    y    cells
					+
						fromNumD2 (+ x 1)    y    cells
						+
							fromNumD2 (- x 1) (+ y 1) cells
							+
								fromNumD2    x    (+ y 1) cells
								fromNumD2 (+ x 1) (+ y 1) cells



fn cellFlipper num
	x num
	y num
	sourceWidth  num
	sourceHeight num
	?
		fromNumD2 x y cells
		?
			< 2 (neighborCount x y)
			0
			?
				> 3 (neighborCount x y)
				0
				1
		?
			= 3 (neighborCount x y)
			1
			0

state cells numD2
	?
		currentFrame
		dynamicFillNumD2 cellFlipper cells
		randomBoolFillNumD2
			buildNumD2 windowWidth windowHeight


fn cellsToVideo byte4
	x num
	y num
	sourceWidth  num
	sourceHeight num
	?
		fromNumD2 x y cells
		liveColor
		deadColor


videoOut
	dynamicFillByte4D2
		cellsToVideo
		buildByte4D2
			windowWidth
			windowHeight

