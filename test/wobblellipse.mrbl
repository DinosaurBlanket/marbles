

|frameRate
|	30
videoWidth
	300
videoHeight
	300

var fillColor B4
	B4from01  0.2  0.8  0.4  1.0
var backColor B4
	B4from01  0.1  0.3  0.6  1.0


fn ellipseFiller B4
	xPos N1
	yPos N1
	sourceWidth  N1
	sourceHeight N1
	?
		>=
			1
			+
				/
					sqr (- xPos (/ sourceWidth 2))
					sqr (/ sourceWidth 2)
				/
					sqr (- yPos (/ sourceHeight 2))
					sqr (/ sourceHeight 2)
		fillColor
		backColor


@enter

var wobbleRate N1
	0.01

state sinOscOut01 N1
	/
		+ 1 (sin (/ currentFrame (/ frameRate wobbleRate)))
		2


fn ellipseBoundary N4
	N4
		* sinOscOut01 (/ videoWidth 3)
		* (- 1 sinOscOut01) (/ videoHeight 3)
		-
			videoWidth
			* 2 (* sinOscOut01 (/ videoWidth 3))
		-
			videoHeight
			* 2 (* (- 1 sinOscOut01) (/ videoHeight 3))

videoOut
	mapInB4D2
		ellipseFiller
		ellipseBoundary
		fillB4D2
			backColor
			buildB4D2
				videoWidth
				videoHeight

