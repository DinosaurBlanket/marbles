

videoWidth
	256
videoHeight
	256
windowWidth
	512
windowHeight
	512


fn automataSelector N1
	< (% currentFrame 40) 30

fn liveColor B4
	?
		automataSelector
		B4from01  0.22  0.92  0.44  1.0
		B4from01  0.92  0.22  0.44  1.0

var deadColor B4
	B4from01  0.08  0.22  0.55  1.0

fn initialCellFiller N1
	x N1
	y N1
	sourceWidth  N1
	sourceHeight N1
	> 3 (% x 5)


@enter

fn neighborCount N1
	x N1
	y N1
	add8
		fromN1D2 (- x 1) (- y 1) cells
		fromN1D2    x    (- y 1) cells
		fromN1D2 (+ x 1) (- y 1) cells
		fromN1D2 (- x 1) (+ y 1) cells
		fromN1D2    x    (+ y 1) cells
		fromN1D2 (+ x 1) (+ y 1) cells
		fromN1D2 (- x 1)    y    cells
		fromN1D2 (+ x 1)    y    cells

fn cgol N1
	x N1
	y N1
	sourceWidth  N1
	sourceHeight N1
	?
		fromN1D2 x y cells
		?
			either
				> 2 (neighborCount x y)
				< 3 (neighborCount x y)
			0
			1
		?
			= 3 (neighborCount x y)
			1
			0


fn seeds N1
	x N1
	y N1
	sourceWidth  N1
	sourceHeight N1
	?
		= 2 (neighborCount x y)
		1
		0

fn cellFlipper N1 & N1 N1 N1 N1
	?
		automataSelector
		cgol
		seeds

state cells N1D2
	?
		currentFrame
		mapN1D2 cellFlipper cells
		mapInN1D2
			initialCellFiller
			N4
				* 3 (/ videoWidth  8)
				* 3 (/ videoHeight 8)
				/ videoWidth  4
				/ videoHeight 4
			buildN1D2 videoWidth videoHeight


fn cellsToVideo B4
	x N1
	y N1
	sourceWidth  N1
	sourceHeight N1
	?
		fromN1D2 x y cells
		liveColor
		deadColor


videoOut
	mapB4D2
		cellsToVideo
		buildB4D2
			videoWidth
			videoHeight

